<!DOCTYPE html>
<html lang="en">
  <head>

    <meta charset="utf-8">
    <title>basic fastcgi with racket</title>
    <meta name="description" content="The objective is to implement the most basic working fastcgi protocol in pure racket.  How it works:  The minimal fastcgi implementation will look like:   wait on a tcp connection read all input data   write output hello world like data   close connection...">
    <meta name="author"      content="antoine">
    <meta name="keywords"    content="racket">
    <meta name="viewport"    content="width=device-width, initial-scale=1.0">
    <link rel="icon"      href="/favicon.ico">
    <link rel="canonical" href="http://antoineB.github.io/blog/2015/06/02/basic-fastcgi-with-racket/">

    <link rel="prev" href="/blog/2015/07/06/mapping-data-from-sql-result-to-struct/">
    <!-- CSS -->
    <link rel="stylesheet" type="text/css" href="/css/bootstrap.min.css">
    <link rel="stylesheet" type="text/css" href="/css/pygments.css">
    <!-- <link rel="stylesheet" type="text/css" href="/css/scribble.css"> -->
    <link rel="stylesheet" type="text/css" href="/css/custom.css">
    <!-- Feeds -->
    <link ref="alternate" type="application/atom+xml"
          href="/feeds/all.atom.xml" title="Atom Feed">
    <link ref="alternate" type="application/rss+xml"
          href="/feeds/all.rss.xml" title="RSS Feed">
  </head>
  <body>
    <!-- A standard Twitter Bootstrap nav bar -->
    <header class="navbar navbar-default navbar-inverse"
            role="banner">
      <div class="container">
        <div class="navbar-header">
          <button type="button"
                  class="navbar-toggle"
                  data-toggle="collapse"
                  data-target=".our-nav-collapse">
            <span class="icon-bar"></span>
            <span class="icon-bar"></span>
            <span class="icon-bar"></span>
          </button>
          <a href="/index.html" class="navbar-brand">antoineB's blog</a>
        </div>
        <div class="collapse navbar-collapse our-nav-collapse"
             role="navigation">
          <ul class="nav navbar-nav">

            <li class="dropdown">
              <a href="#" class="dropdown-toggle" data-toggle="dropdown">
                Tags <b class="caret"></b></a>
              <ul class="dropdown-menu">
                <li><a href="/index.html">All Posts</a></li>

<li><a href="/tags/javascript.html">javascript</a></li>

<li><a href="/tags/language.html">language</a></li>

<li><a href="/tags/php.html">php</a></li>

<li><a href="/tags/racket.html">racket</a></li>
              </ul>
            </li>
            <li>
              <a href="/About.html">About</a>
            </li> 
            <li><a href="/feeds/all.atom.xml">Atom</a></li>
            <li><a href="/feeds/all.rss.xml">RSS</a></li>
          </ul>
        </div>
      </div>
    </header>
    <div class="container">
      <div class="row">

        <!-- Main column -->
        <div id="content" class="col-md-10">





          <article class="hentry">
  <header>
    <h1><span class="entry-title">basic fastcgi with racket</span> <small>
<time datetime="2015-06-02" pubdate="true">2015-06-02</time></small></h1>
  </header>
   <div class="entry-content">

<p class="lead">The objective is to implement the most basic working fastcgi protocol in pure racket.</p>

<br />

<h2 id="how-it-works">How it works</h2>

<p>The minimal <a href="fastcgi.com/devkit/doc/fcgi-spec.html">fastcgi implementation</a> will look like:</p>

<ol>
 <li>wait on a tcp connection</li>
 <li>read all input data</li>
 <li>write output hello world like data</li>
 <li>close connection</li></ol>
<!-- more-->

<p>A request is composed of parts denoted here as <em>segment</em>. Each segment is composed of a header of 8 bytes and some data of 
 <var>N</var> bytes and padding of 
 <var>M</var> bytes where 
 <var>N</var> and 
 <var>M</var> &gt;=0.</p>

<p>The header is:</p>

<ol>
 <li>fastcgi version (don&rsquo;t care)</li>
 <li>segment type (init request, end request, stdin, data, stdout, strerr etc&hellip;)</li>
 <li>request id</li>
 <li>request id (don&rsquo;t care, it is useful when the connection is shared for multiple request, not the case here)</li>
 <li>data length</li>
 <li>data length</li>
 <li>padding length</li>
 <li>void</li></ol>

<h2 id="racket-implementation">Racket implementation</h2>

<p>The decoding incoming bytes part:</p>

<div class="brush: racket">
 <pre><code>#lang racket

;; to store the input data
(struct request [segment-id stdin data params] #:mutable #:transparent)

(define (create-empty-request)
  (request #f (open-output-bytes) (open-output-bytes) (open-output-bytes)))

(define (decode-segment request in out)
  ;; don't care if read-bytes return eof
  (define header (read-bytes 8 in))
  (define segment-type (bytes-ref header 1))
  (define segment-id (+ (arithmetic-shift (bytes-ref header 2) 8)
                          (bytes-ref header 3)))
  ;; don't care if read-bytes return eof
  (define data-length (+ (arithmetic-shift (bytes-ref header 4) 8)
                         (bytes-ref header 5)))
  (define data (read-bytes data-length in))
  ;; consume the padding bytes
  (void (read-bytes (bytes-ref header 6) in))

  (printf "segment type: ~a\n" segment-type)
  (printf "id: ~a\n" segment-id)
  (printf "data length: ~a\n" data-length)
  (printf "~a\n" data)

  ;; segment-id = 0 is a special case where the web server and the fastcgi
  ;; process exchange information like n° of maximum connection so... we don't
  ;; care.
  (when (= 0 segment-id)
    (raise "unimplemented"))

  (printf "~a\n" request)

  (case segment-type
    ;; fcgi beginrequest
    ((1) (set-request-segment-id! request segment-id))
    ;; fcgi params
    ((4) (display data (request-params request)))
    ;; fcgi stdin
    ((5) (if (= 0 data-length) ; Not sure if it's the rigth way but use that to
                               ; know if the webserver input is over.
             (respond request out)
             (display data (request-stdin request))))
    ;; fcgi data
    ((8) (display data (request-data request)))
    ;; fcgi get values
    ;; fcgi abort request
    ((2 9) (raise "unimplemented"))
    (else (raise "undocumented type"))))</code></pre></div>

<p>The response:</p>

<div class="brush: racket">
 <pre><code>(define (build-header type id data-length padding-length)
  (bytes 1 type
         (arithmetic-shift id -8) (modulo id 256)
         (arithmetic-shift data-length -8) (modulo data-length 256)
         padding-length 0))

(define (respond request out)
  ;; header of the stdout
  (write-bytes (build-header 6 (request-segment-id request) 64 0) out)
  ;; the stdout data
  (write-bytes
   #"Content-type: text/html\r\n\r\n&lt;html&gt;&lt;body&gt;hello fcgi&lt;/body&gt;&lt;/html&gt; "
   out)
  ;; an empty stdout data to indicate we have finish the stdout stream
  (write-bytes (build-header 6 (request-segment-id request) 0 0) out)
  ;; the end request type
  (write-bytes (build-header 3 (request-segment-id request) 8 0) out)
  (write-bytes (bytes 0 0 0 0 0 0 0 0) out)
  (close-output-port out))</code></pre></div>

<p>The server:</p>

<div class="brush: racket">
 <pre><code>(module+ main
  (require racket/tcp)
  (define server (tcp-listen 1025))
  (let loop-main ()
    (let-values ([(in out) (tcp-accept server)]
                 [(request) (create-empty-request)])
      (let loop ()
        (if (port-closed? out)
            ;; the end of the request
            (close-input-port in)
             (begin
               (decode-segment request in out)
               (loop))))
      )
    (loop-main)))</code></pre></div>

<p>Nginx configuration:</p>

<div class="brush: nginx">
 <pre><code>location ~ \.rkt$ {
    fastcgi_pass 127.0.0.1:1025;
    include fastcgi.conf; # this add several parameters to be pass
                          # to the fastcgi process like http method.
}</code></pre></div>

<p>go to <em>http://localhost/anything.rkt</em> that display &ldquo;hello fcgi&rdquo;</p>

<h2 id="display-the-http-headers">Display the HTTP headers</h2>

<p>First of all we need to extract the parameters form the request-params field, it is <a href="http://www.fastcgi.com/devkit/doc/fcgi-spec.html#S3.4">name, value pair</a> with their respective length before.</p>

<div class="brush: racket">
 <pre><code>(define (decode-params in)
  (define (decode-param-length in)
    (define b3 (read-byte in))
    (cond [(equal? b3 eof) eof]
          [(&gt; b3 #x7f)
           (let ([others (read-bytes 3 in)])
             (+
              (arithmetic-shift (bitwise-and b3 #x7f) 24)
              (arithmetic-shift (bytes-ref others 2) 16)
              (arithmetic-shift (bytes-ref others 1) 8)
              (bytes-ref others 0)))]
          [else b3]))
  (let loop ([result empty])
    (let ([length0 (decode-param-length in)])
      (if (equal? eof length0)
          result
          (let ([length1 (decode-param-length in)])
            (loop
             (cons
              (cons (read-bytes length0 in)
                    (read-bytes length1 in))
              result)))))))</code></pre></div>

<p>Apply this decoder and present them, also do convenience functions.</p>

<div class="brush: racket">
 <pre><code>(define (fcgi-end-request id out)
  (write-bytes (build-header 3 id 8 0) out)
  (write-bytes (bytes 0 0 0 0 0 0 0 0) out)
  (close-output-port out))

(define (write-fcgi-stdout id data out [close #f])
  (define data-length (bytes-length data))
  (define padding-length (if (= 0 (modulo data-length 8)) 0 (- 8 (modulo data-length 8))))
  (write-bytes (build-header 6 id data-length padding-length) out)
  (write-bytes data out)
  (when (not (= 0 padding-length))
    (write-bytes (make-bytes padding-length) out))
  (when close
    (write-bytes (build-header 6 id 0 0) out)))

(define (respond request out)
  ;; get the data from request
  (define params (decode-params
                  (open-input-bytes (let ([port (request-params request)])
                                      (close-output-port port)
                                      (get-output-bytes port)))))
  (define data (open-output-bytes))
  (write-bytes #"Content-type: text/html\r\n\r\n&lt;html&gt;&lt;body&gt;" data)
  (write-bytes #"&lt;table&gt;" data)
  (for ([param params])
    (write-bytes #"&lt;tr&gt;&lt;td&gt;" data)
    (write-bytes (car param) data)
    (write-bytes #"&lt;/td&gt;&lt;td&gt;" data)
    (write-bytes (cdr param) data)
    (write-bytes #"&lt;/td&gt;&lt;/tr&gt;" data))
  (write-bytes #"&lt;/table&gt;" data)
  (write-bytes #"&lt;/body&gt;&lt;/html&gt;" data)
  (close-output-port data)

  (write-fcgi-stdout (request-segment-id request)
                     (get-output-bytes data)
                     out
                     #t)
  ;; the end request type
  (fcgi-end-request (request-segment-id request) out))</code></pre></div>

<h2 id="performance-expectation">Performance expectation</h2>

<p>To have an idea where the n° of requests per seconds I used <code>ab -c 25 -n 10000</code> on several configurations:</p>

<ul>
 <li>racket web/server (a web server written in pure racket): 1300</li>
 <li>nginx + chicken fastcgi binding: 10000</li>
 <li>nginx + php fpm (5 worker thread seems to be the optimal on my 2 core cpu): 7700</li>
 <li>nginx: 18000</li>
 <li>my stuff: 300</li></ul>

<p>Wait what 300, something is wrong obviously, furthermore <code>ab</code> get stuck most of the time at the last 1000 request. After suspecting the gc I find that I didn&rsquo;t read the doc for <a href="http://docs.racket-lang.org/reference/tcp.html?q=tcp-liste#%28def._%28%28lib._racket%2Ftcp..rkt%29._tcp-listen%29%29">tcp-listen</a> and changed the max-allow-wait to something like 1024. And get then 2500 request/second, even more by removing printf to 6500 r/s.</p>

<p><a href="/code/fastcgi.rkt">The whole code</a></p>

<h2 id="adding-threads">Adding threads</h2>

<p>The server can still handle one request at a time waiting for the first to finish. Using the green thread of racket will enable to execute request concurrently.</p>

<p>The change to the code is straightforward just create a new thread for <strong>every</strong> incoming request.</p>

<div class="brush: racket">
 <pre><code>(module+ main
  (require racket/tcp)
  (define server (tcp-listen 1025 1024 #t))
  (let loop-main ()
    (let-values ([(in out) (tcp-accept server)]
                 [(request) (create-empty-request)])
      (thread
       (lambda ()
         (let loop ()
           ;; similate work
           (sleep sleeping-time)
           (if (port-closed? out)
               ;; the end of the request
               (close-input-port in)
               (begin
                 (decode-segment request in out)
                 (loop))))
         ))
      )
    (loop-main)))</code></pre></div>

<p>To show the difference from before lets simulate some work with the sleeping-time variable and use <code>ab -c 100 -n 10000</code> to run our experiments.</p>

<p>From the previous <strong>non</strong> threaded code:</p>

<ul>
 <li>
  <var>sleeping-time</var>=0.0001 -&gt; 1600 request/s</li>
 <li>
  <var>sleeping-time</var>=0.001 -&gt; 196 request/s</li></ul>

<p>With threaded code:</p>

<ul>
 <li>
  <var>sleeping-time</var>=0.01 -&gt; 1900 request/s</li></ul>

<p>Even for heavier work the threaded code is faster.</p>

<h3 id="a-round-robin-strategy">A round robin strategy</h3>

<p>Instead of create a new thread and a new request every time we have an incoming request, create a bunch of thread and select them through a round robin approach.</p>

<div class="brush: racket">
 <pre><code>(module+ main
  (require racket/tcp)
  (define server (tcp-listen 1025 1024 #t))

  (define max-thread 100)
  (define round-robin
    (let ([robin 0])
      (lambda ()
        (define rob (modulo (add1 robin) max-thread))
        (set! robin rob)
        rob)))


  (define (create-thread request)
  (define (reset-request! request)
    (set-request-segment-id! request #f)
    (set-request-stdin! request (open-output-bytes))
    (set-request-data! request (open-output-bytes))
    (set-request-params! request (open-output-bytes)))

  (thread
     (lambda ()
       (let main-loop ()
         (define in (thread-receive))
         (define out (thread-receive))
         (reset-request! request)
         (let loop ()
           (sleep 0.0001)
           (if (port-closed? out)
               ;; the end of the request
               (close-input-port in)
               (begin
                 (decode-segment request in out)
                 (loop))))
         (main-loop)))))

  (define thread-vec (for/vector ([i (range max-thread)])
                       (create-thread (create-empty-request))))

  (let loop-main ()
    (let-values ([(in out) (tcp-accept server)]
                 #;[(request) (create-empty-request)]
                 [(rob) (round-robin)])

      (thread-send (vector-ref thread-vec rob) in)
      (thread-send (vector-ref thread-vec rob) out)
      )
    (loop-main)))</code></pre></div>

<p>I have no idea which approach is better, if I increase the number of concurrent connection in apache bench the round robin seems to do better, but this is maybe just due to the fact that round robin set a fixed maximum number of threads.</p>

<p><a href="/code/fastcgi-robin.rkt">round robin code</a></p>

<h2 id="gambit-implementation">Gambit implementation</h2>

<p>Quickly do a gambit implementation and get 6800 request/second.</p>

<p><a href="/code/fastcgi-gambit.scm">gambit code</a></p>
   </div>
   <footer>
  </footer>
</article>
        </div>
      </div>
      <footer>
        <hr/>
        <p>Site generated
        by <a href="https://github.com/greghendershott/frog">Frog</a>,
        the <strong>fr</strong>ozen bl<strong>og</strong> tool.</p>
        <p>Using <a href="http://twitter.github.com/bootstrap/index.html">Bootstrap</a>.</p>
      </footer>
    </div>
    <!-- </body> JS -->
    <!-- <script type="text/javascript" src="//code.jquery.com/jquery.min.js"></script> -->
    <!-- <script type="text/javascript" src="/js/bootstrap.min.js"></script> -->
  </body>
</html>